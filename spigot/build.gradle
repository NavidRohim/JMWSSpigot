plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '3.0.2'
    id 'com.gradleup.shadow' version '9.3.0'
}

group = 'me.brynview.navidrohim'
version = '0.0.1-alpha.1'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.11-R0.2-SNAPSHOT")

    implementation project(':common')
    implementation 'dev.jorel:commandapi-spigot-plugin:11.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
}

shadowJar {
    archiveBaseName.set("${rootProject.name}-spigot")
    archiveClassifier.set("")

    relocate("dev.jorel", "me.brynview.navidrohim")
}

tasks {
  runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.21.11")
  }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
